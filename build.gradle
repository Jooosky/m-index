/*
 * Copyright © 2012 Karel Rank All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without modification,
 * are permitted provided that the following conditions are met:
 *  Redistributions of source code must retain the above copyright notice, this
 *   list of conditions and the following disclaimer.
 *  Redistributions in binary form must reproduce the above copyright notice, this
 *   list of conditions and the following disclaimer in the documentation and/or
 *   other materials provided with the distribution.
 *  Neither the name of Karel Rank nor the names of its contributors may be used to
 *   endorse or promote products derived from this software without specific prior
 *   written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS “AS IS” AND
 * ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
 * WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
 * DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE LIABLE FOR
 * ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
 * (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
 * LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON
 * ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
 * SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

apply plugin: 'java'
apply plugin: 'project-report'
apply plugin: 'idea'

description = 'M-Index implementation in Java'
version = '0.2'

sourceCompatibility = '1.7'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.mapvine:gradle-cobertura-plugin:1.0'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile "net.jcip:jcip-annotations:1.0"
    compile "org.slf4j:slf4j-api:1.7.2"
    compile "org.apache.commons:commons-math3:3.1"
    testCompile "org.perf4j:perf4j:0.9.16"
    testCompile "org.hamcrest:hamcrest-all:1.3"
    testCompile 'org.testng:testng:6.7'
    testRuntime 'ch.qos.logback:logback-classic:1.0.9'
}


test {
    jvmArgs "-XX:-UseSplitVerifier"
    useTestNG() {
        excludeGroups 'longRunning,perf'
    }
}

ext.perfTestCounter = 0;

ext.defaultTestJvmArgs = ['-Xmn2048m', '-verbose:gc', '-XX:+PrintGCDetails', '-XX:+PrintGCApplicationStoppedTime', '-XX:+PrintGCTimeStamps', '-Xloggc:gc.log']
ext.testsJvmArgs =
    [['-XX:+AggressiveOpts', '-XX:-AggressiveOpts'], ['-XX:+UseCompressedOops', '-XX:-UseCompressedOops'], ['-XX:+UseG1GC']].combinations()


testsJvmArgs.each { testJvmArgs ->
    task "perfTest${perfTestCounter++}"(type: Test) {
        include '**/*PerfTest*'

        dependsOn { test }
        minHeapSize = "6192m"
        maxHeapSize = "6192m"

        // Reference file for M-Index performance testing
        systemProperty 'mindex.reference.file', 'src/test/resources/ColorHistogram.txt'

        jvmArgs = (testJvmArgs << defaultTestJvmArgs).flatten()

        logger.quiet("Created ${name} with jvmArgs: ${jvmArgs}")

        useTestNG()

        beforeTest { descriptor ->
            logger.lifecycle("Running test: " + descriptor)
        }
    }
}

task perfTest {
    dependsOn {
        tasks.findAll { task -> task.name ==~ 'perfTest\\d+' }
    }
}
